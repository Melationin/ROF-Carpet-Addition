plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

def minecraftVer = stonecutter.current.version
def accesswidener = stonecutter.eval(minecraftVer, ">=1.21") ? "1_21_4.accesswidener"
		: stonecutter.eval(minecraftVer, ">=1.20") ? "1.20.accesswidener"
		: "1.19.accesswidener"


base {
	archivesName = "${project.archives_base_name}-${minecraftVer}"

}


repositories {
	maven {
		url 'https://masa.dy.fi/maven'

	}
	maven{
		url "https://api.modrinth.com/maven"
	}
}

loom {
	accessWidenerPath = rootProject.file("src/main/resources/accesswideners/${accesswidener}")

	splitEnvironmentSourceSets()

	mods {
		"rof_carpet_addition" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and create separate worlds
	}

}

dependencies {
	// To change the versions see the gradle.properties file

	minecraft "com.mojang:minecraft:${stonecutter.current.project}"
	mappings "net.fabricmc:yarn:${property("yarn_mappings")}:v2"
	modImplementation "net.fabricmc:fabric-loader:${property("loader_version")}"
	modImplementation "maven.modrinth:carpet:${property("carpet_core_version")}"
	implementation 'it.unimi.dsi:fastutil:8.5.18'

	annotationProcessor project(":processor")
	compileOnly project(":processor")
}

processResources {
	inputs.property "minecraft", stonecutter.current.version

	filesMatching("fabric.mod.json") {
		expand( [
				"minecraftVer": stonecutter.current.version,
				aw_file: accesswidener,
				"modVer": mod_version
				])

	}
}

java {

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21


}

jar {
	inputs.property "archivesName", "${project.base.archivesName}-${minecraftVer}"

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {

	}
}

stonecutter {
	// Subproject configuration
}

